buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}
repositories {
    maven {
        name "chickenbones"
        url "http://chickenbones.net/maven/"
    }
    maven {
        name "mvnmrtjp"
        url "http://files.projectredwiki.com/maven"
    }
    maven {
        name "dvs1"
        url "http://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "ic2"
        url = "http://maven.ic2.player.to/"
    }
    ivy {
        name 'ComputerCraft'
        artifactPattern "http://addons-origin.cursecdn.com/files/2244/605/[module][revision].[ext]"
    }
    ivy {
        name 'Thaumcraft'
        artifactPattern "http://addons-origin.cursecdn.com/files/2227/552/[module]-[revision].[ext]"
    }
    ivy {
        name 'CoFHCore'
        artifactPattern "http://addons-origin.cursecdn.com/files/2245/632/[module]-[revision].[ext]"
    }
    ivy {
        name 'AppleMilkTea2'
        artifactPattern "http://addons-origin.cursecdn.com/files/2274/437/[module]-[revision].[ext]"
    }
}

apply plugin: 'forge'

version = "2.4.8.a-1.7.10"//${project.minecraft.version}"
group= "shift.SextiarySector2" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "SextiarySector2"

ext.libMCEconomy2 = "./lib/mceconomy2"

minecraft {
    version = "1.7.10-10.13.4.1448-1.7.10"
    runDir = "eclipse"
    assetDir = "eclipse/assets"

    replace '@VERSION@', project.version
    replace '@MC_VERSION@', version
}

[compileJava,compileApiJava, compileTestJava,recompMinecraft].each{
    it.options.encoding = 'UTF-8'
    it.options.compilerArgs += ['-source', '1.7', '-target', '1.7']
}

configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
	java {
		srcDir project.libMCEconomy2 + "/src/main/java"
	}
	resources {
		srcDir project.libMCEconomy2 + "/src/main/resources"
	}
    }
    test {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
}

dependencies { 
    compile fileTree(dir: 'api', include: '*.jar')
    compile fileTree(dir: 'lib', include: '*.jar')

    //provided
    provided "codechicken:CodeChickenLib:1.7.10-1.1.3.138:dev"
    provided "codechicken:CodeChickenCore:1.7.10-1.0.7.47:dev"
    provided "codechicken:NotEnoughItems:1.7.10-1.0.5.118:dev"
    
    provided "tconstruct:TConstruct:1.7.10-1.7.0.build734:deobf"
    
    provided group: 'cofh', name: 'CoFHCore', version: '[1.7.10]3.0.3B2-294-dev', ext: 'jar'
    provided group: 'thaumcraft', name: 'Thaumcraft', version: '1.7.10-4.2.3.5', ext: 'jar'
    provided group: 'ComputerCraft', name: 'ComputerCraft', version: '1.74', ext: 'jar'
    provided group: 'AppleMilkTea2', name: 'AppleMilkTea2', version: '1.7.10_2.9c', ext: 'jar'
    
    provided "net.industrial-craft:industrialcraft-2:2.2.780-experimental:api"

    provided "codechicken:ForgeMultipart:1.7.10-1.2.0.345:dev"
}

processResources
{

    //exclude 'mcmod.info'

    
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs[0]) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs[0]) {
        exclude 'mcmod.info'
    }
}

jar {

  into('src/shift/sextiarysector/api') {
    from 'src/main/java/shift/sextiarysector/api'
  }

  exclude('shift/mceconomy2/**')
  exclude('assets/mceconomy2/**')

  manifest {
    attributes 'FMLCorePlugin' : 'shift.sextiarysector.asm.SSCore','FMLCorePluginContainsFMLMod':'true'
  }

}

task devJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.allSource
    exclude('shift/mceconomy2/**')
    exclude('assets/mceconomy2/**')
    classifier = 'dev'
}
 
artifacts {
    archives devJar
}

task downloadMods(type: Copy) {
  from configurations.provided
  into 'api'
}
